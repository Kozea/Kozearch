# Maintainer: Simon Sapin <simon dot sapin at exyr dot org>
pkgname=('python-weasyprint' 'python2-weasyprint')
pkgver=0.6.1
pkgrel=1
pkgdesc="Converts web documents (HTML, CSS, SVG, ...) to PDF."
arch=('any')
url="http://weasyprint.org/"
license=('AGPL3')
source=("http://pypi.python.org/packages/source/W/WeasyPrint/WeasyPrint-${pkgver}.tar.gz")
makedepends=('python' 'python2')
md5sums=('6f401b5787719f5a8f60dd5fcbf8c519')


# XXX as of 2012-03-01, python-lxml is for Python2.
# Update dependencies below when/if this changes.

package_python-weasyprint() {
  depends=('python' 'pango>=1.29.3' 'python-gobject' 'python-cairo'
           'python3-lxml' 'python-pystacia' 'python-cssutils>=0.9.9'
           'python-cairosvg>=0.3')
  cd "$srcdir/WeasyPrint-$pkgver"
  python3 setup.py install --root="$pkgdir/" --optimize=1
}

package_python2-weasyprint() {
  depends=('python2' 'pango>=1.29.3' 'python2-gobject' 'python2-cairo'
           'python-lxml' 'python2-pystacia' 'python2-cssutils>=0.9.9'
           'python2-cairosvg>=0.3')
  cd "$srcdir/WeasyPrint-$pkgver"
  python2 setup.py install --root="$pkgdir/" --optimize=1
  rm "$pkgdir/usr/bin/weasyprint"  # Avoid a conflict: only keep the Py3 one.
}
