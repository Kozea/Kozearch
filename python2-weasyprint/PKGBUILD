# Maintainer: Simon Sapin <simon dot sapin at exyr dot org>
pkgname='python2-weasyprint'
pkgver=0.6.1
pkgrel=3
pkgdesc="Converts web documents (HTML, CSS, SVG, ...) to PDF."
arch=('any')
url="http://weasyprint.org/"
license=('AGPL3')
source=("http://pypi.python.org/packages/source/W/WeasyPrint/WeasyPrint-${pkgver}.tar.gz")
makedepends=('python2')
depends=('python2' 'pango>=1.29.3' 'python2-gobject' 'python2-cairo'
         'python2-lxml' 'python2-pystacia' 'python2-cssutils>=0.9.9'
         'python2-cairosvg>=0.3')
md5sums=('6f401b5787719f5a8f60dd5fcbf8c519')


package() {
  cd "$srcdir/WeasyPrint-$pkgver"
  python2 setup.py install --root="$pkgdir/" --optimize=1
  script="$pkgdir/usr/bin/weasyprint"
  mv "$script" "${script}2"  # Avoid a conflict with python-weasyprint
}
