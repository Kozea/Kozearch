# Maintainer: Simon Sapin <simon dot sapin at exyr dot org>
pkgname=('python-cairosvg' 'python2-cairosvg')
pkgver=0.3.1
pkgrel=1
pkgdesc="A Simple SVG Converter for Cairo"
arch=('any')
url="http://cairosvg.org/"
license=('LGPL3')
source=("http://pypi.python.org/packages/source/C/CairoSVG/CairoSVG-${pkgver}.tar.gz")
makedepends=('python' 'python2')
md5sums=('51359fa55bc73a6bed788050cae015b5')


package_python-cairosvg() {
  depends=('python' 'python-cairo')
  cd "$srcdir/CairoSVG-$pkgver"
  python3 setup.py install --root="$pkgdir/" --optimize=1
}

package_python2-cairosvg() {
  depends=('python2' 'python2-cairo')
  cd "$srcdir/CairoSVG-$pkgver"
  python2 setup.py install --root="$pkgdir/" --optimize=1
  rm -rf "$pkgdir/usr/bin"  # Avoid a conflict: only keep the Py3 executable.
}
